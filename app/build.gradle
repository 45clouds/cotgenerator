apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.jon.cotgenerator"
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 150
        versionName "1.5.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + System.currentTimeMillis() + "L)"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        setProperty("archivesBaseName", "cotgenerator-$versionName")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0'
    }
    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                javalite {}
            }
        }
    }
    generatedFilesBaseDir = "$projectDir/src/main/java/generated"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation group: 'androidx.appcompat', name: 'appcompat', version: '1.1.0'
    implementation group: 'androidx.constraintlayout', name: 'constraintlayout', version: '1.1.3'
    implementation group: 'androidx.lifecycle', name: 'lifecycle-extensions', version: '2.2.0'
    implementation group: 'com.google.android.material', name: 'material', version: '1.1.0'
    implementation group: 'androidx.recyclerview', name: 'recyclerview', version: '1.1.0'

    /* Preferences */
    implementation group: 'androidx.preference', name: 'preference', version: '1.1.1'
    implementation group: 'com.jaredrummler', name: 'colorpicker', version: '1.1.0'

    /* GPS access */
    implementation group: 'com.google.android.gms', name: 'play-services-location', version: '17.0.0'

    /* Permission management */
    implementation group: 'pub.devrel', name: 'easypermissions', version: '3.0.0'

    /* Room Database */
    implementation group: 'androidx.room', name: 'room-runtime', version: '2.2.5'
    implementation group: 'androidx.room', name: 'room-rxjava2', version: '2.2.5'
    annotationProcessor group: 'androidx.room', name: 'room-compiler', version: '2.2.5'
    testImplementation group: 'androidx.room', name: 'room-testing', version: '2.2.5'

    /* RxJava */
    implementation group: 'io.reactivex.rxjava2', name: 'rxjava', version: '2.2.19'
    implementation group: 'io.reactivex.rxjava2', name: 'rxandroid', version: '2.1.1'

    /* HTTP */
    implementation group: 'com.squareup.retrofit2', name: 'retrofit', version: '2.9.0'
    implementation group: 'com.squareup.retrofit2', name: 'converter-gson', version: '2.9.0'

    /* Utils */
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
    implementation group: 'commons-io', name: 'commons-io', version: '2.7'

    /* Logging */
    implementation group: 'com.jakewharton.timber', name: 'timber', version: '4.7.1'

    /* Protobuf */
    implementation group: 'com.google.protobuf', name: 'protobuf-lite', version: '3.0.1'

    /* Testing */
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
