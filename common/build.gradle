apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'
apply from: '../dependencies.gradle'

android {
    compileSdkVersion versions.targetSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versions.versionCode
        versionName versions.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        setProperty("archivesBaseName", "common-$versionName")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$versions.protoc"
    }
    plugins {
        javalite {
            artifact = "com.google.protobuf:protoc-gen-javalite:$versions.protoc"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                javalite {}
            }
        }
    }
    generatedFilesBaseDir = "$projectDir/src/main/java/generated"
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    /* General */
    implementation group: 'com.google.android.material', name: 'material', version: "$versions.material"
    implementation group: 'androidx.constraintlayout', name: 'constraintlayout', version: "$versions.constraintlayout"

    /* Preferences */
    implementation group: 'androidx.preference', name: 'preference', version: "$versions.preference"
    implementation group: 'com.jaredrummler', name: 'colorpicker', version: "$versions.colorpicker"

    /* Permission management */
    implementation group: 'pub.devrel', name: 'easypermissions', version: "$versions.easypermissions"

    /* GPS access */
    implementation group: 'com.google.android.gms', name: 'play-services-location', version: "$versions.play_services_location"

    /* Room Database */
    implementation group: 'androidx.room', name: 'room-runtime', version: "$versions.room"
    implementation group: 'androidx.room', name: 'room-rxjava2', version: "$versions.room"
    annotationProcessor group: 'androidx.room', name: 'room-compiler', version: "$versions.room"
    testImplementation group: 'androidx.room', name: 'room-testing', version: "$versions.room"

    /* RxJava */
    implementation group: 'io.reactivex.rxjava2', name: 'rxjava', version: "$versions.rxjava"
    implementation group: 'io.reactivex.rxjava2', name: 'rxandroid', version: "$versions.rxandroid"

    /* HTTP */
    implementation group: 'com.squareup.retrofit2', name: 'retrofit', version: "$versions.retrofit"
    implementation group: 'com.squareup.retrofit2', name: 'converter-gson', version: "$versions.retrofit"

    /* Utils */
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: "$versions.commons_collections4"
    implementation group: 'commons-io', name: 'commons-io', version: "$versions.commons_io"

    /* Logging */
    implementation group: 'com.jakewharton.timber', name: 'timber', version: "$versions.timber"

    /* Protobuf */
    implementation group: 'com.google.protobuf', name: 'protobuf-lite', version: "$versions.protobuf_lite"

    /* Testing */
    testImplementation "junit:junit:$versions.junit"
    androidTestImplementation "androidx.test.ext:junit:$versions.androidx_junit"
    androidTestImplementation "androidx.test.espresso:espresso-core:$versions.espresso"
}