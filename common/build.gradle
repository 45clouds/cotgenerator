apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion versions.targetSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versions.versionCode
        versionName versions.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        setProperty("archivesBaseName", "common-$versionName")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$versions.protoc"
    }
    plugins {
        javalite {
            artifact = "com.google.protobuf:protoc-gen-javalite:$versions.protoc"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                javalite {}
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    /* Kotlin */
    implementation "androidx.core:core-ktx:$versions.core_ktx"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"

    /* UI */
    api "com.google.android.material:material:$versions.material"
    api "androidx.preference:preference:$versions.preference"
    api "com.jaredrummler:colorpicker:$versions.colorpicker"
    api "pub.devrel:easypermissions:$versions.easypermissions"

    /* Room Database */
    implementation "androidx.room:room-runtime:$versions.room"
    implementation "androidx.room:room-rxjava2:$versions.room"
    annotationProcessor "androidx.room:room-compiler:$versions.room"
    testImplementation "androidx.room:room-testing:$versions.room"

    /* Networking */
    api "io.reactivex.rxjava2:rxjava:$versions.rxjava"
    api "io.reactivex.rxjava2:rxandroid:$versions.rxandroid"
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$versions.retrofit"

    /* Utils */
    api "com.jakewharton.timber:timber:$versions.timber"
    implementation "com.google.android.gms:play-services-location:$versions.play_services_location"
    implementation "com.google.protobuf:protobuf-lite:$versions.protobuf_lite"
    implementation "commons-io:commons-io:$versions.commons_io"

    /* Testing */
    testImplementation "junit:junit:$versions.junit"
    testImplementation "org.mockito:mockito-core:$versions.mockito"
    testImplementation "com.github.IvanShafran:shared-preferences-mock:$versions.shared_preferences_mock"
    testImplementation "org.hamcrest:hamcrest:$versions.hamcrest"
    androidTestImplementation "androidx.test.ext:junit:$versions.androidx_junit"
    androidTestImplementation "androidx.test.espresso:espresso-core:$versions.espresso"
}
